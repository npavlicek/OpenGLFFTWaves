#version 460 core

#define PI 3.1415926535897932384626433

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0, rgba16f) uniform image2D spectrum;

layout(location = 0) uniform int patchSize;

float phillips(vec2 k)
{
	float A = 1.;

	vec2 wind = vec2(10., 20.0);
	float wind_mag = length(wind);
	wind = normalize(wind);

	float k_mag = length(k);
	k = normalize(k);

	float g = 9.81;
	float L = pow(wind_mag, 2) / g;

	float res = exp(-1/pow(k_mag * L, 2)) / pow(k_mag, 4);
	res *= pow(abs(dot(wind, k)), 8);
	res *= A;

	return res;
}

void main() {
	uvec3 id = gl_GlobalInvocationID;

	ivec2 size = imageSize(spectrum);

	float n = id.x - size.x / 2.0;
	float m = id.y - size.y / 2.0;

	float kx = 2 * PI * n / patchSize;
	float ky = 2 * PI * m / patchSize;

	float ph = sqrt(phillips(vec2(kx, ky))) / sqrt(2);

	vec4 val = imageLoad(spectrum, ivec2(id.xy));

	float newR = val.r * ph;
	float newG = val.g * ph;

	if (isnan(newR)) newR = 0.0;
	if (isnan(newG)) newG = 0.0;

	imageStore(spectrum, ivec2(id.xy), vec4(newR, newG, 0, 0));
}
